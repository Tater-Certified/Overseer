plugins {
    alias(libs.plugins.shadow)
}

dependencies {
    // Tooling
    implementation('dev.neuralnexus:conditional-mixins:0.1.1')
    compileOnly('dev.neuralnexus:entrypoint-spoof:0.1.11')

    // Configurate
    implementation('org.spongepowered:configurate-hocon:4.2.0-SNAPSHOT')
    implementation('io.leangen.geantyref:geantyref:1.3.15')

    // Mixin
    compileOnly('org.spongepowered:mixin:0.8.5')
    compileOnly('org.ow2.asm:asm-tree:6.2')
}

tasks.withType(ProcessResources).configureEach {
    filesMatching([
            'plugin.yml',
            'bungee.yml',
            'fabric.mod.json',
            'META-INF/mods.toml', 'mcmod.info', 'pack.mcmeta',
            'META-INF/sponge_plugins.json',
            'velocity-plugin.json'
    ]) {
        expand project.properties
    }
}

java.disableAutoTargetJvm()

shadowJar {
    dependencies {
        exclude('META-INF/maven/')
        exclude('META-INF/services/')
        exclude('META-INF/versions/')
        exclude('plugin.yml')
        exclude('bungee.yml')
        exclude('fabric.mod.json')
        exclude('META-INF/neoforge.mods.toml')
        exclude('mcmod.info')
        exclude('pack.mcmeta')
        exclude('META-INF/sponge_plugins.json')
        exclude('velocity-plugin.json')
    }
    // Tooling
    relocate('dev.neuralnexus', 'ca.taterland.tatercertified.overseer.lib.neuralnexus')

    // Configurate
    relocate('org.spongepowered.configurate', 'ca.taterland.tatercertified.overseer.lib.configurate')
    relocate('com.typesafe.config', 'ca.taterland.tatercertified.overseer.lib.typesafe.config')
    relocate('io.leangen.geantyref', 'ca.taterland.tatercertified.overseer.lib.geantyref')

    minimize() {
        exclude(dependency(':common'))
    }

    archiveFileName = "${project_id}-common-full-${version}.jar"
}

artifacts {
    archives(shadowJar)
}
